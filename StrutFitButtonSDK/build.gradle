import com.vanniktech.maven.publish.SonatypeHost
import com.vanniktech.maven.publish.AndroidSingleVariantLibrary

plugins {
    id 'com.android.library'
    id("com.vanniktech.maven.publish") version "0.30.0"
}

android {
    namespace "strutfit.button"

    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue 'string', 'sfButtonUrl', 'https://sizinglambda.strut.fit/'
            resValue 'string', 'webViewBaseUrl', 'https://scan.strut.fit/'
            resValue 'string', 'conversionUrl', 'https://pixellambda.strut.fit/'
        }
        debug {
            resValue 'string', 'sfButtonUrl', 'https://sizinglambda.dev.strut.fit/'
            resValue 'string', 'webViewBaseUrl', 'https://scan.dev.strut.fit/'
            resValue 'string', 'conversionUrl', 'https://pixellambda.dev.strut.fit/'
        }
        staging {
            initWith debug
            resValue 'string', 'sfButtonUrl', 'https://sizinglambda.strut.fit/'
            resValue 'string', 'webViewBaseUrl', 'https://scan.strut.fit/'
            resValue 'string', 'conversionUrl', 'https://pixellambda.strut.fit/'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.1.3'
}

mavenPublishing {
    // the first parameter represents which variant is published
    // the second whether to publish a sources jar
    // the third whether to publish a javadoc jar
    configure(new AndroidSingleVariantLibrary("release", true, true))

    // You must increase this version and run the following command to release to maven-central:
    //  .gradlew publishAndReleaseToMavenCentral --no-configuration-cache
    coordinates("fit.strut", "strutfit-android-sdk", "8.0.0")

    pom {
        name.set("StrutFit Android SDK")
        description.set("StrutFit Android SDK")
        inceptionYear.set("2024")
        url.set("https://github.com/StrutFit/AndroidSDK")

        licenses {
            license {
                name.set("GPLv3")
                url.set("https://www.gnu.org/licenses/gpl-3.0.en.html")
            }
        }

        developers {
            developer {
                id.set("jake-strutfit")
                name.set("Jake Thomas")
                email.set("jake@strut.fit")
            }
        }

        scm {
            url.set("https://github.com/StrutFit/AndroidSDK")
        }
    }

    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
    signAllPublications()
}